"Lord of the Read" by Simon, Tessa and Marcela

Use American dialect. Use the serial comma.
Release along with an interpreter.

Chapter 1 - The Word

The real world is a region.
The Old room is in the real world.

The story is a region.
The Shire, the Bag End, Rivendell and Mount Doom are in the story.
The Shire is west of Rivendell.

Inside from the shire is the Bag End.

Section 1 - Introduction - Start

The description of the old room is "Something here calls to you, like a whisper in the wind or the faint rustling of the worn pages of a well-loved book. Around and above you the walls are covered in paint. You can’t seem to remember how you made it here. There are no doors, no way for you to get out, not even a window to suggest where in the world you are." 

The walls are scenery in the old room. "Scattered painted scenes chase each other without order or thought, a majestic golden eagle flies over mountain peaks, an elf stares hauntingly at a village perched on a waterfall and on the ceiling someone too small to be a man smiles forlornly, behind him ships ready to sail in the unknown.". 
Understand "paint" or "paintings" as walls. 


A book is a kind of thing. A book has a text called the quote. A book can be known or unknown. A book is usually unknown. 

An old table is in the old room. The mysterious book is a book on the old table. 
The description of the old table is "A sturdy wooden thing, with carvings along it's legs of delicate leaves and vines.". 
The description of the mysterious book is "A hardback book, it's red cover frayed at the edges. If it weren't for the golden script on the spine no one would look twice at it. The title is enclosed in a circle: '[italic type]The Fellowship of the Ring[roman type]', underneath a name known by most: '[italic type]J. R. R. Tolkien[roman type]'."
The quote of the mysterious book is "Holding your breath, you carefully open the book.[paragraph break]'[italic type]...One Ring to rule them all, One Ring to find them,[line break]One Ring to bring them all and in the darkness bind them.[line break]In the Land of Mordor where the Shadows lie...[roman type]'[line break]". 

Understand "The Fellowship of the Ring" or "Fellowship of the Ring" as book.  

Section 2 - Bag End

The description of Bag End is "You find yourself in a cavernicolous and airy room. Doors, windows and ceilings are round, like if someone has carved holes in the earth itself. Outside you can hear the laughter of children and the chirping of birds.".
A little table, a trunk, a cupboard and a cushy armchair are in Bag End.
The trunk is openable, closed and not portable. The description of the trunk is "An old and heavy thing, on which the name BAGGINS is roughly carved.". 
The green book is a book on the little table. The map is a book on the little table.
The cupboard is openable, closed and not portable. A loaf of bread, an apple and a peach are in the cupboard. A loaf of bread, an apple and a peach are edible things. 
The cushy armchair is an enterable supporter. The cushy armchair is not portable. It is pushable between rooms. 

Understand "chair" as armchair. Understand "cup" as cupboard.

The description of the green book is "As green as fine summery grass, the title of the book stands boldly on its front '[italic type]Surviving Your First High and Other Ways to Have the Best Time of Your Life With Weed[roman type]'."
The quote of the green book is "[if the green book is unknown]You open [the noun] at a random page.[otherwise]You open [the noun] at the page you previously earmarked.[end if][paragraph break][italic type]This is the story of the fellow from out of time and space [paragraph break]There was, once upon a time,[line break]a young lost fellow without dime.[line break]Taken by a book, he said[line break]but the hobbits just thought him mad.[line break]He looked around, and he went east,[line break]then west, then back, then flew on a great beast.[line break]For the life of him he could not remember,[line break]how he came to be or if it was July or December.[line break]He spoke with many, fought with few, [line break]but in the end, he decided to just enjoy the view.[line break]He rolled his weed, sat on a chair,[line break]took out his pipe and smoked without flair.[paragraph break]Here ends the story of the young fellow,[line break]he sure must have been relaxed and mellow,[line break]because soon he disappeared [line break]leaving behind not even a hair of beard,[line break]without a first breakfast, a second or a third;[line break]whispers say that he returned to his place of birth. [line break]But alas, no hobbit ever saw the fellow again,[line break]and so, with the best weed and the pipe most plain,[line break]they sometimes meet, under the summery sun,[line break]to enjoy a good smoke and pun,[line break]in honor of the fellow,[line break]who decided to stay mellow,[line break]despite being lost, and alone,[line break]adventurous and a bit injury prone. [paragraph break]The one that smoked and then was just gone.[roman type][paragraph break][if the noun is unknown]You earmark the page and close the book, baffled.[otherwise]You mumble the rhymes to yourself a couple of times and then close [the noun].[end if][line break]".
The description of the map is "Pretty old, it was probably drawn by hand. You are not an expert, but you should be able to read it.".
The quote of the map is "Incredibly detailed, it is signed by one of your ancestors. Bag End stands proudly (if a bit larger than the scale would allow), in the middle of the Shire. To the east you can see great mountains and the city of Rivendell and in the south, encased in a smudge of black ink, there is Mount Doom. [line break]". 

A pipe and a bag of money are in the trunk.
The description of the pipe is "A true hobbit [pipe]. Long, carved from wood, with glinting, leafy decorations on the bowl and the shank. You can almost hear Bilbo's voice in your head saying '[italic type]No simple tobacco shall be smoked in my [pipe] Frodo. It was carved and created specifically for weed and [bold type]only [italic type]weed[roman type]'."

[THE RING]
The Ring is a wearable thing. "A simple band of gold, when heated it reveals a script in Elvish runes. You can feel a dark power emanating from it.". The Ring has a number called usage limit. The usage limit of the ring is usually 10.
The indefinite article of the ring is "The".

Section 3 - The Shire

The description of the Shire is "Round, colorful doors are set in the sprawling green hills, from the tops of which the smoke of small chimneys hints at homely meals shared at round tables. Some hobbits are venturing the paths between smials, others are enjoying the sun.[if Gandalf is in The Shire] [Gandalf] towers above all even while sitting on his favorite chair.[end if][if Sam & Pip are in The Shire] You can see [Sam & Pip] lounging on the grass.[end if][if there is no character in The Shire] Your friends must be in some other places at the moment.[end if]".

Pipe-weed is a thing. "This, this is...very high quality. From the smell alone you can tell that if smoked, this unassuming clump of greenery may very well gift you the high of a lifetime.". Understand "weed" as pipe-weed.

A character is a kind of person. Gandalf, Sam & Pip are character in the shire.

Section 4 - Rivendell

The description of Rivendell is "At the edge of a narrow gorge, the peaceful elven town of [Rivendell] seems almost to sit on the river Bruinen. Wherever you are you can hear water streaming by or roaring down steep cliffs. Maybe before exploring you should look for someone to talk to.". 
The Elf Lord is a character in Rivendell.
The pedestal is a supporter in Rivendell. The carrying capacity of the pedestal is 3. 
An old sword and the mithril shirt are on the pedestal. The mithril shirt is a wearable thing. 
The description of the old sword is "A beautiful thing that would look extremely well tied around your hip. On the blade there is an inscription in what you assume to be elvish, maybe [Gandalf] would know how to translate it?". 
The description of the mithril shirt is "Light to the touch, the strange chain mail shines softly. You don't know much about this stuff, but it must be special, or at the very least worth a pretty coin."


Section 5 - Mount Doom - end

The description of the Mount Doom is "The heat is blistering, suffocating. Somewhere below you licks of flames run along barren rock and molten lava churns at the mouth of the active [volcano]. [If the player holds the ring] The ring hisses, dark things, promises of power and greed, it would even implore and beg if it was capable of such a thing. This could be the end of your story, the end of your adventure, will there be ships to whisk you away once you throw the ring to the flames of [Mount Doom]?[otherwise] Even through you smoked out brain you remember that there was something important, something you should do here, a ring maybe? You did not forget the ring somewhere, right? RIGHT?! [end if][line break]".

The volcano is an open container in Mount Doom.

Chapter 2 - Settings

A person can be visible or hidden. A person is usually visible.
A person can be waiting the council.
A person can be chatting. A person is usually not chatting.
A person can be followed. A person is usually not followed.
A person can be able to go to Rivendell.
A character have a table name called the Responses Table. A character have a table name called the Seconde Responses Table. A character have a list of texts called Answered List.
A character can be following. A character is usually not following.
A character has a list of room called Already Visited Room.
		

Section 1 - Actions

[READING]
Understand the command "read" as something new.
Reading is an action applying to a thing. Understand "read [something]" as reading.
Check reading when the noun is not a book: instead say "You can't read that.".
Carry out reading: 
	say "[The quote]"; 
	now the noun is known.
Understand "open [a book]" as reading. 

[SMOKING]
Smoking is an action applying to one visible thing.
Understand "smoke [something]" as smoking.

Check smoking when the noun is not pipe-weed: 
	if the noun is the pipe:
		say "It’s empty, unfortunately." instead; 
	otherwise:
	 	say "Sounds like something you might have tried in high school." instead. 

Check smoking when the pipe is not held: instead say "How do you plan on doing it... with your hands? A pipe may be a good idea."

Carry out smoking: say "You light [the noun] with your lighter and inhale
deeply. You can almost feel the air moving inside your lungs, [the noun] sinking deep in your blood and your brain. Life is such a strange and wonderous thing and what matters if you’re stuck inside a version of [italic type]Lord of the Rings[roman type] where everyone treats you like an idiot? You know how this story begins and ends, if you could just turn to one of the last chapters, you could show them, show them all how the story of Frodo Baggins should have gone. You can see the pages in your mind, you turn and turn and turn them…and you find yourself teleported to Mount Doom!".

[EATING]
Check eating when the noun is edible: 
	if the noun is a loaf of bread: 
		say "Such a thick and crunchy crust! You cannot resist and eat the whole thing in three gigantic bites." instead; 
	otherwise: 
		say "You eat [the noun] with gusto." instead.

[FOLLOW]
Every turn:
	repeat with NPC running through characters which are following:
		now current conversation table is the Responses Table of NPC;
		let current location be the location of the player;
		let path be the best route from the location of NPC to the current location;
		[évite d'ajouter la localisation deux fois]
		if the location of NPC is not listed in the Already Visited Room of NPC:
			add the location of NPC to the Already Visited Room of NPC;
		[Déplace le joueur]
		if path is not nothing:
			now the player is followed;
			if the player is hidden:
				now NPC is not following;
			otherwise:
				silently try NPC going path;
				[Ajoute du dialogue si le PNJ visite la salle pour la 1èere fois]
				if current location is not listed in the Already Visited Room of NPC:
					repeat through the Table of Characters Dialogues:
						if the Character entry is NPC:
							if the Room entry is the current location:
								copy Table entry to The Responses Table of NPC;
								[list options from the Table entry;]
	if the player is followed:
		if the player is hidden:
			say "[italic type]You look around but no one seems to have followed you anymore.";
		otherwise:
			let followers be the list of characters which are following;
			say "[italic type][followers] followed you.";
		now the player is not followed;

[SAVOIR QUOI DIRE]
List-asking is an action applying to one thing.
Understand "ask [thing]" as list-asking.
Understand "ask [thing] about" as list-asking.

Check list-asking when the noun is not a person:
	instead say "It’s not [italic type]that[roman type] kind of story. No talking to animals or inanimate objects here.".
Report list-asking:
	let T be the Responses Table of the noun;
	if T is empty:
		say "This person has nothing to say.";
	otherwise:
		list options from T.
	
To list options from (T - a table name):
	let N be the number of filled rows in T;
	if N is 0:
		say "This person has nothing to say.";
	otherwise:
		say "[if the player is chatting or the player is followed][line break][italic type]Now you could also ask[otherwise] You could ask [the noun][End if] about ";
		repeat with X running from 1 to N minus 1:			
			say "[index in row X of T][if N is greater than 2],[end if] ";
		if N is greater than 1:
			say "or ";
		say "[index in row N of T].".

[DIALOGUER]
Understand "talk to [someone]" as a mistake ("To start a conversation, you need to use ASK [the noun] ABOUT something. Type 'ask [the noun] (about)' to see the available topics.").
Instead of telling someone about something, try asking the noun about it. Instead of answering the noun that something, try asking the noun about it.

Check asking someone about something:
	if the player is hidden: 
		say "You ask [the noun] about [the topic understood] but [if the noun is Sam & Pip]they don't[otherwise]he doesn't[end if] seem to see or hear you." instead.

After asking someone about something:
	now current conversation table is the Responses Table of the noun;
	now the player is chatting;
	if the topic understood is a topic listed in current conversation table:
		say "[Response entry]";
		if there is a subtopics entry:
			copy subtopics entry to current conversation table;
			list options from subtopics entry;
		if there is a Action entry:
			if "[Action entry]" is "move to rivendell by eagle":
				if player consents:
					say "[Result entry]";
					add "[Index entry]" to the Answered List of the noun;
					blank out the whole row;
					now the noun is following;
					move the player to rivendell;
				otherwise:
					say "As you please.";
			otherwise if "[Action entry]" is "move to Rivendell by foot":
				if player consents:
					say "[Result entry]";
					add "[Index entry]" to the Answered List of the noun;
					blank out the whole row;
					now the noun is following;
				otherwise:
					say "As you please.";
			otherwise if "[Action entry]" is "get weed":
				say "[Result entry]";
				add "[Index entry]" to the Answered List of the noun;
				now the player have pipe-weed;
				blank out the whole row;
		otherwise:
			add "[Index entry]" to the Answered List of the noun;
			blank out the whole row;
		sort current conversation table in labels order;
	otherwise:
		if the topic understood is listed in the Answered List of the noun:
			choose a random row in the Seconde Responses Table of the noun;
			say "[Seconde Response entry]";
		otherwise:
			say "I don’t see what you mean, sorry.";
	now the player is not chatting.
				
[PUTTING ON THE RING]
Before wearing something which is not carried by the player:
		silently try taking the noun;
		if the player is not holding the noun, stop the action;
		say "You take [the noun] but...".
		
After wearing:
	if the noun is not the ring:
		say "... you are not sure about the utility of wearing [it].[line break]";
	otherwise:
		say "This feels weird, you feel weird…your vision isn't the same, what happened?! You lift your hand in front of your eyes but…you can see through it! Are you invisible?!".

Before wearing when the noun is the ring:
	say "You have a bad feeling about this ring. Are you really sure you want to do that?";
	if player consents:
		say "You slowly put the ring on your finger, wary of the bad feeling.";
		now the player is hidden;
	otherwise:
		say "Yeah, it’s better this way.";
		stop the action.

[REMOVE THE RING]
Before taking off when the noun is the ring:
	say "Do you really want to let go of all this power that is surging in you? You feel so good and so strong right now.";
	if player consents:
		now the player is visible;
	otherwise:
		say "Yeah, it’s better this way.";
		stop the action;
		
After taking off when the noun is the ring:
	say "After removing the ring, you feel like you've escaped great danger.";


[WAITING FOR COUNCIL]
Instead of going to a room when the player is waiting the council:
	say "You should wait for the Council.";
Instead of smoking when the player is waiting the council:
	say "There is a time and a place for smoking. Now is not that time (but it could be the place).".
	
[THROWING THINGS INTO THE LAVA]
Understand "lava" or "the lava/the volcano" or "Mount Doom" or "flames" as the volcano.
Instead of inserting something into the volcano:
	say "You throw [the noun] into the lava... you see [it] burning. A little anticlimactic if you ask me.";
	now the noun is nowhere.

Instead of inserting the player into the volcano:
	if the player carries the ring or the player is wearing the ring:
		say "Unable to throw this powerful ring into the flames, you decide to disappear with it into the magma. Holding the ring in the palm of your hand, you get ready to perform the last dive of your short life. After having gathered all your courage, you close your eyes and let yourself drop forward ...But you don't fall.[line break]Turning around, you see a strange creature holding you back. Encouraged by this act of kindness emanating from the hostile creature, you think about your homeland and your friends…This quest has already taken so much from you, it should not have your life as well. You decide to throw the ring in the volcano instead.";
	otherwise:
		say "That would probably work if you were, you know, actually carrying the ring.[line break]".

Section 2 - speech mechanism

To copy (first table - a table name) to (second table - a table name):
	repeat through first table:
		let copied Topic be topics;
		if there is a Topic entry, now the copied Topic is the Topic entry;
		let copied Response be "blank";
		if there is a Response entry, now the copied Response is the Response entry;
		let copied Index be "blank";
		if there is a Index entry, now the copied Index are the Index entry;
		let copied Action be "blank";
		if there is a Action entry, now the copied Action are the Action entry;
		let copied Result be "blank";
		if there is a Result entry, now the copied Result are the Result entry;
		let copied subtopics be second table;
		if there is a subtopics entry, now the copied subtopics are the subtopics entry;
		let copied labels be 0;
		if there is a subtopics entry, now the copied subtopics are the subtopics entry;
		choose a blank row in the second table;	
		now Topic entry is copied Topic;
		now Response entry is copied Response;
		now Index entry is copied Index;
		if copied Action is not "blank", now Action entry is copied Action;
		if copied Result is not "blank", now Result entry is copied Result;
		if copied subtopics is not second table, now subtopics entry is copied subtopics;
		now labels entry is copied labels.
		
Current conversation table is a table name that varies.
	
Section 3 - Speech Table

Understand "him/himself" or “them/themselves” or "who/what are you" or "his/their name/origin/capacity" or "his/their life" as "[About]".
Understand "life/univers" or "the life/universe" as "[philosophical questions]". 
Understand "Sam" or "Sam & Pip" or "Pip" as "[Sam]".
Understand "weed/pipe-weed/pipe" or "the weed/pipe" as "[weed]".
understand "story/quest" or "the story/quest" as "[quest]".
Understand "council" or "elf council" or "the council" or "the elf council" as "[council]".
Understand "the weather/climate/elements/day" or "weather/climate/elements/day" as "[weather]".
Understand "me/you/myself" or "what I/you m/am doing here" as "[me]".
Understand "book/books" or "the book/books" as "[books]".
Understand "the/this/my ring" or "ring" as "[ring]".
Understand "volcano/mount/doom" or "mount doom" or "the volcano/mount" or "the mount doom" as "[mount doom]".

Table of Characters Dialogues
Character	Room	Table
Sam & Pip	The Shire	Table of Sam Responses
Sam & Pip	Rivendell	Table of Rivendell Sam Responses
Gandalf	The Shire	Table of Gandalf Responses
Gandalf	Rivendell	Table of Rivendell Gandalf Responses


Table of Gandalf Responses
Topic	Response	Index	Action	Result	subtopics	labels
"[me]"	"'Sir, who I am?'... Did you smoke something? Are you alright? Is this some sort of test? You are Frodo Baggins, of course, hobbit of the Shire.[line break]"	"you"	--	--	Table of Gandalf Topics	0
with 11 blank rows.

Table of Gandalf Topics
Topic	Response	Index	Action	Result	subtopics	labels
"[About]"	"I am Gandalf the Grey. I was sent to Middle Earth to help destroy the ring that you now hold. I am an [italic type]Istar[roman type], a wizard of old.[line break]"	"him"	--	--	--	1
"[weather]"	"A lovely summery day, Frodo. The shadows of war are far but getting closer, soon even the Shire will know the end of its peace.[line break]"	"the weather"	--	--	--	1
"[philosophical questions]"	"You shall not distract yourself with nonsensical and philosophical questions. If you are here to waste my time, go read a book.[line break]"	"life"	--	--	Table of Gandalf Clues	1


Table of Gandalf Clues
Topic	Response	Index	Action	Result	subtopics	labels
"[books]"	"Ah, the keepers of knowledge. I don’t have any here with me at the moment, but I would bet my beard that Bilbo left you some interesting reads at Bag End.[line break]"	"books"	--	--	--	2
"[weed]"	"Mmh… some of the finest weed is actually grown here in the Shire. If you manage to find some, be sure to share it with you dear friend Gandalf.[line break]"	"pipe-weed"	--	--	--	2
"[quest]"	"A great adventure that will take you to places unknown. A long time ago the ring you now hold was created by Sauron, a great evil, to rule over Middle Earth. After a bloody war the ring was cut from its master’s hand and lost to the ages. Somehow your uncle Bilbo found it and passed it on to you Frodo. You are the one, the chosen, he who must go to Mount Doom and destroy the ring once and for all. Before the world is plunged in darkness.[line break]"	"the quest"	--	--	Table of Eagle Fail	2

Table of Eagle Fail
Topic	Response	Index	Action	Result	subtopics	labels
"[ring]"	"The object that you now have in your possession has many names Frodo: the One Ring, Isidur’s Bane, the Ring of Power. It is a very dark and very powerful artifact, one that can only be destroyed where it was created: in the fire of Mount Doom.[line break]"	"the ring"	--	--	--	3
"[mount doom]"	"The great volcano lies south of here, beyond the mountains. It is the only place where the ring may be vanquished, the final destination to your adventure. I could accompany you there… may I suggest we use the Eagles? They are great beasts and should take us to Mount Doom in no time at all. Would you like to leave right now?[line break]"	"Mount Doom"	"move to rivendell by eagle"	"An easy shortcut! How lucky you are![line break] You mount on a beautiful Eagle and soon you find yourself flying above the green Shire. Is that really Sam down there, he looks so little, so small, maybe a little too miniscule. Are we sure we are safe flying so high? DON’T LOOK DOWN, DON’T…[paragraph break]It looks like you are too weak for this mode of transportation, we should stop before you throw up on yourself and the Eagle (she might eat you, they really like to keep themselves clean).[line break]You are now in Rivendell...[paragraph break]"	--	3

Table of Rivendell Gandalf Responses
Topic	Response	Index	Action	Result	subtopics	labels
"[me]"	"Never in my life have I seen someone look so green. I brought you to Rivendell before you threw up on poor Mildred, she was not happy.[line break]"	"me"	--	--	--	0
"Rivendell"	"A beautiful town built by the elves. You should be able to rest and recuperate here."	"Rivendell[line break]"	--	--	--	0
"Mildred"	"The young Eagle took you as far as Rivendell but she refused to allow you on her back ever again. I am afraid that you should find another way to continue on your quest.[line break]"	"Mildred"	--	--	--	0
"[quest]"	"[if the player is waiting the council]The Elf Council is still in session. I think you should wait and hear what they have to say. [otherwise]The Elf Council has deemed you too weak to continue. I think it best to return to the Shire (west).[end if][line break]"	"the quest"	--	--	Table of Rivendell Gandalf Council Topics	0
with 2 blank rows.

Table of Rivendell Gandalf Council Topics
Topic	Response	Index	Action	Result	subtopics	labels
"[council]"	"[if the player is waiting the council]They shall decide of your destiny Frodo, if after the Great Eagle Fail (as it is now known) they decide that you shan’t continue, I would not blame them. [otherwise]It may seem unfair to you, young Frodo, but the elves have survived in Middle Earth for thousands of years. It would be wise to do as they say.[end if][line break]"	"the Elf Council"	--	--	--	1
"[mount doom]"	"Still to the south. Why the ring had to fall in the hands of such a weak creature is beyond me…[line break]"	"Mount Doom"	--	--	--	1

Table of Seconde Gandalf Responses
Seconde Response
"I already talked to you about that."
"Are you ok Frodo? You seem to be more stupid than usual..."
"I already gave you the answer! You fool..."
"Yeah yeah, I'm busy little boy, stop asking me the same questions again and again."

The Responses Table of Gandalf is Table of Gandalf Responses.
The Seconde Responses Table of Gandalf is Table of Seconde Gandalf Responses.

Table of Sam Responses
Topic	Response	Index	Action	Result	subtopics	labels
"[About]"	"Frodo did you hit your head? I’m Sam, that’s Pip, we have been friends for ages.[line break]"	"them"	--	--	Table of Sam Topics	0
with 6 blank rows.

Table of Sam Topics
Topic	Response	Index	Action	Result	subtopics	labels
"[weed]"	"'Well…we stole, I mean borrowed! We borrowed some from old Dinodas while he was drunk. Would you like some?'[line break]"	"pipe-weed"	"get weed"	"[line break][italic type]Sam & Pip give you some nice pipe-weed.[line break]"	--	1
"[quest]"	"'Oh, you mean the ring? The one that you should destroy? That is some serious business Pip, don’t you think?'[line break] 'Very serious, Sam…'[line break] 'Dangerous as well, I have heard stories of orcs lying in wait to eat unassuming hobbits.'[line break] 'Maybe it would be better to ask someone for help, but who?'[line break] 'What about the Elves? I think Rivendell lies east of here, they could probably help.'[line break] 'Well Sam, we cannot really send him out there alone, now, can we?'[line break] 'Of course Pip, you’re right. Should we accompany you Frodo?'[paragraph break]"	"the quest"	"move to Rivendell by foot"	"[italic type]You can now go east, to the elven city of Rivendell. As it should be, Sam & Pip will accompany you on this great adventure.[roman type][paragraph break]"	--	1

Table of Rivendell Sam Responses
Topic	Response	Index	Action	Result	subtopics	labels
"Rivendell"	"Such a wonderful place Frodo! We can bathe in the river and eat the weird elf bread and just enjoy the view for a while.[line break]"	"Rivendell"	--	--	--	0
"[council]"	"[if the player is waiting the council]Last we heard they were still deliberating, but I bet that they will soon be out.[otherwise]'The lady was not really nice but look on the positive side! We get to go back to the Shire (west) and enjoy some peace and tranquility, what say you Pip?'[line break] 'Oh Sam, I can already see us, lunging in the sun, enjoying some sandwiches and a good smoke. That is the life, isn’t it Sam?'[line break]'Yes it is, it definitely is.'[end if][line break]"	"the Elf Council"	--	--	--	0
"[ring]"	"We don’t know, you were the one holding it, maybe you should check that you still have it.[line break]"	"the ring"	--	--	--	0

Table of Seconde Sam Responses
Seconde Response
"Frodo, you already asked us that...."
"We would prefer to smoke rather than answering you again and again"
"You seem a bit under the weather Frodo, you need to rest. Come chill with us !"
"’Pip, you want to answer that again?’[line break]’No Sam, I do not.’"

The Responses Table of Sam & Pip is Table of Sam Responses.
The Seconde Responses Table of Sam & Pip is Table of Seconde Sam Responses.

Table of Elf Responses
Topic	Response	Index	Action	Result	subtopics	labels
"[About]"	"I am Elrond, Elf Lord of Rivendell. "	"him"	--	--	Table of Elf Topics	0
"[Sam]"	"[if Sam & Pip are in Rivendell] You speak of the two brave hobbits ho accompanied you here? I think they are resting somewhere in the palace; our people enjoy their company very much. [otherwise] I do not know what a [italic type] Sam [roman type] and [italic type] Pip [roman type] are… [end if][line break]"	"Sam & Pip"	--	--	--	0
"Gandalf"	"[if Gandalf is in Rivendell] The old fool should be here about…just look for the pointy hat amidst all the luscious hair of the elves and you shall find him. [otherwise] The wizard? How did you come to know him? He is always welcome in Rivendell. [end if][line break]"	"Gandalf"	--	--	--	0
with 3 blank rows.

Table of Elf Topics
Topic	Response	Index	Action	Result	subtopics	labels
"[council]"	"[if the player is waiting the council]The Elf Council is still in session, they are deciding if someone such as you is worthy of the quest of the destruction of the ring.[otherwise]The decision of the Elf Council is final and I have to admit I feel somewhat relived that you were not the one chosen for such a task. Go back to the Shire (west), Frodo, enjoy life, have many children, I am sure one of them will be up to the task.[end if][line break]"	"Elf Council"	--	--	Table of Elf Quest Topic	1
"Rivendell"	 "My gem, my precious town built on the river Bruinen, the place we came to call home. It a serene place young Frodo and I will not have you disrupt its peace.[line break]"	"Rivendell"	--	--	--	1
"[mount doom]"	"The volcano lies somewhere south of here, but I do not know if you are ready for such a trip. You will wait for the Elf Council to deliberate, maybe they will find a worthy elf to throw the ring in the flames of Mount Doom.[line break]"	"Mount Doom"	--	--	--	1

Table of Elf Quest Topic
Topic	Response	Index	Action	Result	subtopics	labels
"[quest]"	"[if the player is waiting the council]The Elf Council shall decide if you are fit or not to continue…probably not.[otherwise]Do not insist, hobbit, the Elf Council has deemed you unworthy.[end if][line break]"	"the quest"	--	--	--	2
"[ring]"	"[if player holds the ring] Still tied around your neck I believe, for how long however I do not know. [otherwise] Did you lose one of the most powerful artifacts know to Middle-Earth? YOU ARE A DISGRACE! I HOPE THE COUNCIL THROWS YOU IN THE RIVER! [end if][line break]"	"The ring"	--	--	--	2

Table of Seconde Elf Responses
Seconde Response
"I have already answered that Frodo, I will not repeat myself."
"Frankly I had a bit more faith in the cleverness of hobbits, are you sure that your trip did not damage you permanently?" 
"I have better things to do than answering the same question again and again." 


The Responses Table of Elf Lord is Table of Elf Responses.
The Seconde Responses Table of Elf Lord is Table of Seconde Elf Responses.
		
Chapter 4 - Scenario

Section 0 - Help

Going nowhere is acting confused.

Instead of going nowhere:
	say "[We] [can't go] that way.";
	if acting confused for the fourth turn:
		if the player is in the old room:
			say "[line break](You really just need to READ the book.)";
		otherwise if the player is in bag end:
			say "[line break](You heard some noise OUTSIDE, you maybe can try to go there?)";
		otherwise if the player is in mount doom:
			say "[line break](You know what appened in this story, don't you? Frodo tried to THROW the ring into the lava, or something like that.)";
		otherwise:
			say "[line break](If you are lost, you may be in need of a map... or a book if you are feeling unconventional[if the player carries pipe-weed] Oh well, is that some pipe-weed you are carrying around? Maybe it would be best to chill after all then, where is your pipe? A good smoke sounds like a splendid idea right about now.[end if]')"; 
			
Instead of burning the ring:
	say "[The noun] can only be destroyed by the fiery pits of Mount Doom. But that's the spirit...".
	
Instead of cutting the ring:
	say "[The noun] can only be destroyed by the fiery pits of Mount Doom. But that's the spirit...".
	
Instead of attacking the ring:
	say "[The noun] can only be destroyed by the fiery pits of Mount Doom. But that's the spirit...".
	

Section 1 - Introduction

After reading the mysterious book: 
	say "You can barely make out a few sentences before words and letters begin to move in concentric swirls. You try to take a step back, but your fingers are stuck to the pages.[line break]WOOOOOSH[line break] A great wind pulls at you and the only thing you can do is scream and scream in terror. You look one last time at the beautiful paintings on the walls and then everything goes black.[line break][line break]"; 
	now the mysterious book is nowhere;
	move player to Bag End;
	now the player carries the ring.
	
Section 2 - Going to Rivendell with Sam & Pip

Instead of going to Rivendell when no characters is following:
	say "This is the way for Rivendell, but the story doesn't allow you to go into the woods alone, sorry.[line break]";
	
Section 3 - Wait the council at Rivendell

Council is a scene. Council begins when the player is in Rivendell for the first time.
When Council begins:
	say "The Elf Council is in session now. You should hear from them soon.";
	the ElfCouncil in 3 minutes from now;
	now the player is waiting the council;

At the time when the ElfCouncil:
	say "A tall lady elf, her hands crossed behind her back, approaches you slowly. Her long dress swishes on the marble pavements. ‘I am Lady Amarië, messenger to the Elf Council. Your plea for help in your quest has been discussed at long in the sacred halls of the council, the task of the destruction of the ring is one of great importance to all of Middle-Earth. That said, you are an incredibly weak hobbit Frodo Baggins and we do not think that you will be able to make it to Mount Doom and destroy the ring before Sauron finds you. You are unworthy, we will not help. In a few hundred years, maybe, a brave hobbit will be born to your line, one who will withstand the journey and its perils. In the meantime I advise you to go back to the Shire (west) and enjoy a quiet and peaceful life.’ She turns around and slowly walks away. You would say something but you’re pretty sure your jaw has hit the floor at some point.";
	now the player is not waiting the council.

Section 4 - Go to Mount Doom

Report smoking: 
	now the noun is nowhere;
	move the player to the Mount Doom;
	if the player does not carry the ring:
		end the story saying "[paragraph break]GAME OVER.[paragraph break] You somehow managed to forget the central element to the whole story (which, might we remind you, is called 'The Fellowship of the RING'). We wish to thank you in any case for having played, it has been fun (for us at least).[paragraph break] All the best to you, dear player, and if you want to try again, just remember to not forget the ring. Good luck!";

Section 5 - Go to end	

Instead of inserting the ring into the volcano:
	say "The ring fights you; it thrashes in your fingers, it implores, in its whispered hisses, but you manage to throw it into the lava. You see it slowly melt away in the fires of the volcano. A great weight seems to have lifted from your shoulders.";
	end the story saying "Despite the blistering and suffocating heat a great wind suddenly picks up. You know this feeling of being pulled in different directions, your hair flipping wildly about. [paragraph break]WOOOOOSH[paragraph break]You find yourself in the old room, the beautiful paintings are still there, as is the book, now closed and resting, unassuming, on the table. One thing is different, though, one thing has changed. There is a door now, a simple wooden door that you open with almost giddy satisfaction. As soon as you are outside you breathe a sigh of relief.[line break]Isn’t this library just beautiful? You came in here looking for your favorite book and you somehow got lost in between the racks. There was a…door? You turn around but all you see is a blank wall and nothing more. Mmh…something at the back of your mind whispers of a great adventure…something, with a book...and smoking? You really cannot remember now. Well, it must not be that important. It’s time to get something to eat anyway. You leave, never looking back, but feeling somewhat satisfied with yourself.".		
	
Section 6 - Too Greedy (Ring fail)

Every turn:
	if the player is wearing the ring:
		decrease the usage limit of the ring by 2;
		if the usage limit of the ring is less than 1:
			say "You see a nazgul on the back of a dragon rushing towards you. No time to escape, he picks you up in his claws.[line break]You wanted to go to Mordor, right? Well, you’re there. But yeah, the plan was not to give the ring to Sauron so that he may rule all of Middle Earth, but you already knew that didn't you?";
			end the story saying "[paragraph break]GAME OVER[paragraph break]Your greed for power was your downfall. There is probably an important life lesson to learn here about but who are we to judge. We wish to thank you in any case for having played, it has been fun (for us at least).[paragraph break]All the best to you, dear player, and if you want to try again, just remember to not let the bad guy win. Good luck!";
		otherwise if the usage limit of the ring is less than 5:
			say "You feel the Great Eye following you.";
	otherwise if the usage limit of the ring is less than 10:
		increase the usage limit of the ring by 1;
		
Chapter 5 - Debug

[TELEPROTATION]
teleporting is an action applying to one thing.
Understand "go to [any room]" as teleporting.
Check teleporting:
    if the noun is not a room, say "people can only travel between location" instead.
Carry out teleporting:
	move the player to the noun;
